PROGRAM ::= 'program' 'id' '{' VARS? FUNCTION* MAIN '}'

VARS ::= 'vars' DECLARE

TYPE ::=  'int' | 'float' | 'string' | 'object' | 'bool'

MAIN ::= 'main' '{' VARS? BODY '}'

INSTR ::= ((ASSIGN | FUNC_CALL | PRINT | READ) ';' | CONDITION | CYCLE)

PRINT ::= 'print' '(' EXP (',' EXP)* ')'

READ ::= 'read' '(' 'id' (',' 'id')* ')'

DECLARE ::= (TYPE 'id' (',' 'id')* ';' | 'struct' 'id' STRUCT ';' | 'dict' 'id' DICT ';')+

INIT ::= '=' (EXPRESION | FUNC_CALL)

INIT_DICT ::= '=' '(' ('cte_s' | CTE | 'id') ':' ('cte_s' | CTE | 'id') ')'

PARAM ::= (TYPE 'id' (',' 'id')*) (';' TYPE 'id' (',' 'id')*)* 

FUNCTION ::= 'func' 'id' ('(' PARAM+ ')')? ('returns' TYPE '{' VARS? BODY RETURN)? '{' VARS? BODY '}'

RETURN ::= 'return' EXPRESION ';'

STRUCT ::= (TYPE | 'dict' DICT) '[' 'cte_d' ']' ('[' 'cte_d' ']')?

DICT ::= '(' TYPE ':' TYPE ')'

BODY ::=  (INSTR+)?

ASSIGN ::= 'id' (('[' EXPRESION ']' ('[' EXPRESION ']')?)? INIT | INIT_DICT)

CONDITION ::= 'if' '(' EXPRESION ')' '{' BODY '}' ('else' '{' BODY '}')?

CYCLE ::= FOR_CYCLE | WHLE_CYCLE

WHILE_CYCLE ::= 'while' '(' EXPRESION ')' '{' BODY '}'

FOR_CYCLE ::= 'for' '(' ASSIGN ';' EXPRESION ';' ASSIGN ')'  '{' BODY '}'

CTE ::= 'cte_d' | 'cte_f' | 'true' | 'false'

EXPRESION ::= S_EXP (('or' | 'and') S_EXP)*

S_EXP ::=  EXP (('>' | '>=' | '<' | '<=' | '!=' | '==') EXP)?

EXP ::= TERM (('+' | '-') TERM)*

TERM ::= FACT (('*' | '/') FACT)*

FACT ::= ('cte_s' | 'id' ('[' EXPRESION ']' ('[' EXPRESION ']')? | '.' ('0' | '1'))? | CTE | '(' EXPRESION ')' | FUNC_CALL )

FUNC_CALL ::= 'id' '(' (EXPRESION (',' EXPRESION)*)? ')'